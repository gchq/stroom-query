/*
 * Copyright 2017 Crown Copyright
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
sourceSets {
    // This lets us use different directories for our integration tests.
    integrationTest {
        java {
            
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
    }
}

configurations {
    // This means our integration tests get all the dependencies from our tests and we don't need to specify them twice.
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

// This task lets us run the actual integration tests.
task integrationTest(type: Test) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    testLogging {
        events "standardOut", "started", "passed", "skipped", "failed"
    }
}

// These lines pull the integration test task into our build process.
check.dependsOn integrationTest
integrationTest.mustRunAfter test

// This means the reports from our integration tests won't over-write the reports from our unit tests.
tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

dependencies {
    //--------Project----------
    implementation project(':stroom-query-api')
    implementation project(':stroom-query-common')
    implementation project(':stroom-query-audit')

    //--------Stroom libs------
    implementation libs.eventLogging
    implementation libs.stroomExpression

    //--------3rd party--------
    implementation libs.kafka
    implementation libs.dropwizard_auth
    implementation libs.dropwizard_auth_jwt
    implementation libs.dropwizard_hibernate
    implementation libs.slf4j_api

    implementation libs.junit
    implementation libs.assertj
    implementation 'org.apache.directory.studio:org.apache.commons.io:2.4'
    implementation libs.wiremock

    implementation libs.dropwizard_testing

    integrationTestImplementation libs.dropwizard_core
    integrationTestImplementation libs.jackson_annotations
    integrationTestImplementation libs.dropwizard_flyway
    integrationTestImplementation project(':stroom-query-hibernate')
    integrationTestImplementation project(':stroom-query-testing') // so the ITs can see the classes in main/java
    integrationTestRuntimeOnly libs.mariadb_connector
    integrationTestRuntimeOnly 'org.apache.derby:derby:10.8.2.2'
}

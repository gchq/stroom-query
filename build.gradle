buildscript {
    repositories {
        mavenLocal()
        jcenter()
    }
    dependencies {
        classpath 'ca.cutterslade.gradle:gradle-dependency-analyze:1.1.0'
        classpath 'com.benjaminsproule:swagger-gradle-plugin:1.0.14'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

plugins {
    id 'maven-publish'
}

//if the project has a value for the passed property (i.e from the cmd line via -PpropName=xxx)
//use that, else use a default value
ext.getPropertyOrDefault = { propName, defaultValue -> 
    def val;
    if (project.hasProperty(propName) && project.getProperty(propName) != "unspecified" && project.getProperty(propName) != "") {
        val = project.getProperty(propName)
        println "Using property [$propName] with value [$val]"
    } else {
        val = defaultValue
        println "Property [$propName] has no value, using default value [$val]"
    }
    return val;
}

ext.versions = [
    //----------Stroom-----------------
    stroomQuery: getPropertyOrDefault('version', 'SNAPSHOT'),

    //------Stroom-libs---------------
    stroomExpression: 'v1.5.10',
    eventLogging: 'v3.1.2_schema-v3.1.2',

    //------------3rd-party------------
    dropwizard: '1.3.14',
    jackson: '2.9.9', //in line with dropwizard 1.3.14
    logback: '1.2.3', //in line with dropwizard 1.3.14
    kafka: '0.10.0.1',
    slf4j: '1.7.26', //in line with dropwizard 1.3.14
    swagger: '1.5.16',
    zzDUMMYzz: 'makes sorting this list easier'
]

//dependency strings for use in sub projects
ext.libs = [
    //------Stroom-libs---------------
    eventLogging: "event-logging:event-logging:$versions.eventLogging",
    stroomExpression: "stroom:stroom-expression:$versions.stroomExpression",

    //------------3rd-party------------
    assertj: 'org.assertj:assertj-core:3.8.0',
    dropwizard_core: "io.dropwizard:dropwizard-core:$versions.dropwizard",
    dropwizard_hibernate: "io.dropwizard:dropwizard-hibernate:$versions.dropwizard",
    dropwizard_auth: "io.dropwizard:dropwizard-auth:$versions.dropwizard",
    dropwizard_auth_jwt: "com.github.toastshaman:dropwizard-auth-jwt:1.0.2-0",
    dropwizard_flyway: 'io.dropwizard.modules:dropwizard-flyway:1.0.0-1',
    dropwizard_testing: "io.dropwizard:dropwizard-testing:$versions.dropwizard",
    guava: "com.google.guava:guava:21.0", //>=v21 removes MoreExecutors.sameThreadExecutor() which is needed by curator 2.11
    jackson_annotations: "com.fasterxml.jackson.core:jackson-annotations:$versions.jackson",
    jackson_core: "com.fasterxml.jackson.core:jackson-core:$versions.jackson",
    jackson_databind: "com.fasterxml.jackson.core:jackson-databind:$versions.jackson", 
    junit: "junit:junit:4.12",
    logback_classic: "ch.qos.logback:logback-classic:$versions.logback",
    logback_core: "ch.qos.logback:logback-core:$versions.logback",
    kafka: "org.apache.kafka:kafka-clients:$versions.kafka", //CDH5.10 uses kafka 10.0, Kafka <10.2 is picky about client and server versions.
    mariadb_connector: 'org.mariadb.jdbc:mariadb-java-client:2.0.2',
    mockito_core: "org.mockito:mockito-core:2.0.2-beta",
    slf4j_api: "org.slf4j:slf4j-api:$versions.slf4j",
    swagger_annotations: "io.swagger:swagger-annotations:$versions.swagger",
    ws_rs_api: "javax.ws.rs:javax.ws.rs-api:2.0.1",
    wiremock: 'com.github.tomakehurst:wiremock:2.14.0',
    zzDUMMYzz: "makes sorting easier"
]

// To give us the clean task
apply plugin: 'base'

allprojects {
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'

    group = 'stroom'
    version = versions.stroomQuery
}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    scm {
       url "https://github.com/stroom/stroom-query"
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenLocal()
        jcenter()
        maven { url "https://dl.bintray.com/stroom/event-logging" }
        maven { url "https://dl.bintray.com/stroom/stroom" }
    }

    dependencies {
        testImplementation libs.junit
        testImplementation libs.mockito_core
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifact sourcesJar
                artifact javadocJar
                pom.withXml {
                    def root = asNode()
                    root.appendNode('name', project.name)
                    root.children().last() + pomConfig
                }
            }
        }
    }

    //configuration for the bintray plugin for uploading maven artefacts to bintray
    //see https://github.com/bintray/gradle-bintray-plugin
    //run task bintrayUpload to push the files, assuming BINTRAY_USER/KEY are set as env vars
    bintray {
        //Must never write these to log or system out
        user = System.getenv('BINTRAY_USER') //set in Travis UI
        key = System.getenv('BINTRAY_KEY') //set in Travis UI

        //The maven plugin publications to push to bintray
        publications = ['mavenJava']

        //immediately make the artefacts public
        publish = true

        pkg {
            repo = 'stroom'
            name = 'stroom-query'
            userOrg = 'stroom'
            licenses = ['Apache-2.0']
            vcsUrl = 'https://github.com/gchq/stroom-query.git'
            version {
                name = "${versions.stroomQuery}"
                desc = "stroom-query-${versions.stroomQuery}"
                released  = new Date()
                vcsTag = "${versions.stroomQuery}"
                gpg {
                    //Bintray will self-sign the files
                    sign = true //Determines whether to GPG sign the files. The default is false
                }
            }
        }
    }
}

project(':stroom-query-api') {
    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
    }
}

project(':stroom-query-common') {
    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
    }
}

project(':stroom-query-audit') {
    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
    }
}

project(':stroom-query-hibernate') {
    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
    }
}

project(':stroom-query-testing') {
    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
    }
}

// This is so we can publish the javadoc for all sub projects as a single aggregated set on
// github pages. It is in addition to the javadoc generated for maven publishing.
task aggregatedJavadocs(
    type: Javadoc, 
    description: 'Generate javadocs from all child projects as if it was a single project', 
    group: 'Documentation') {

    doLast {
        println "Created aggregate javadoc"
    }

    //dependsOn project(':stroom-query-api').javadoc
    //dependsOn project(':stroom-query-common').javadoc
    //dependsOn project(':stroom-query-hibernate').javadoc
    //dependsOn project(':stroom-query-audit').javadoc
    //dependsOn project(':stroom-query-testing').javadoc

    destinationDir = file("$buildDir/docs/javadoc")
    title = "$project.name $version API"
    options.author true
    options.links 'http://docs.oracle.com/javase/8/docs/api/'
    options.addStringOption 'Xdoclint:none', '-quiet'

    subprojects.each { proj ->
        proj.tasks.withType(Javadoc).each { javadocTask ->
            dependsOn proj.javadoc

            source += javadocTask.source
            classpath += javadocTask.classpath
            excludes += javadocTask.excludes
            includes += javadocTask.includes
        }
    }
}

project(':stroom-query-api').build.dependsOn aggregatedJavadocs

clean {
    delete "build"
}
